xmlns:xml = "http://www.w3.org/XML/1998/namespace"

NL = [\r\n]+
SP = [ ]
SPO = [ ]+ (MAX)
SPO2 = [ ]*
ANYSP = [\r\n \t]*

BEGTRAN = "<T="
ENDTRAN = "=T>"
BEGP = "<="
ENDP = "=>"

BEGDIV = "<D="
ENDDIV = "=D>"

BEGOPT = "<:"
ENDOPT = ":>"

QUESTION = [\?]?

LINENUM = [0-9]+[/\,a-zA-Z]*[ms0-9]*

//ANYLETTER = [^\/\\\t\^<>_#@~〚〛$\*\&\:\|\!\{\}\?\"¯≡+\u0323]
ANYLETTER = [^\/\\\t\^<>_#@~〚〛$\*\&\:\|\!\{\}\?\"¯≡+\u0323]

// WORD breaks at space and does not allow DOT
WORD = ([^ \/\\\n\r\t\^<>_#@~〚〛$\*\&\,\.\:\=\|\'\!\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

// WORDDIV breaks at space and does not allow DOT - allows +, \,
WORDDIV = ([^ \n\r\t\^<>_#@~〚〛$\*\&\.\:\=\|\'\!\(\)\{\}\?\"¯≡\u0323]+) (MAX)

// WORDS allows space (therefore multiple words) and DOT
WORDS = ([^\/\\\n\r\t\^<>_#@~〚〛$\*\&\,\:\=\|\'\!\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

//WORDSLR allows comma - used to make app lem BL work when lem resp= includes comma (ex. lem resp='cf. 5.11, 3.10')
WORDSLR = ([^\/\\\n\r\t\^<>_#@~〚〛$\*\&\:\=\|\'\!\(\)\{\}\?\"¯≡+\u0323]+) (MAX)

// WORDS 
WORDSTERM = ([^\n\r\t\^<>#@~〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)
//WORDSFOREIGN = WORDSTERM
WORDSFOREIGN = ([^\n\r\t\^<>#@~〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)

//very permisive for free text
WORDSLOOSE = ([^\n\r\t\^<>#@~〚〛$\*\&\=\|\{\}¯≡+\u0323]+) (MAX)

  file
    : [transtag a] = <body>[transtag a]</>
    : [ANYSP s][transtag a]  = <body>[ANYSP s][transtag a]</> 
    : [transtag a] [ANYSP s] = <body>[transtag a]</> [ANYSP s]
    : =
      
  transtag
  
  
    :  [BEGTRAN] "." [WORDDIV d] " " [divtag t] [ENDTRAN] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[divtag t]</>
    >: [BEGTRAN] "." [WORDDIV d] " " [divtag t] [ENDTRAN] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[divtag t]</> [transtag more]
    >: [BEGTRAN] "." [WORDDIV d] " " [divtag t] [ENDTRAN] [NL] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[divtag t]</> [NL] [transtag more]




    :  [BEGTRAN] "." [WORDDIV d] " " [ptag p] [ENDTRAN] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ptag p]</>
    >: [BEGTRAN] "." [WORDDIV d] " " [ptag p] [ENDTRAN] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ptag p]</> [transtag more]
    >: [BEGTRAN] "." [WORDDIV d] " " [ptag p] [ENDTRAN] [NL] [transtag more] = <div xml:lang=[WORDDIV d] type="translation" xml:space="preserve">[ptag p]</> [NL] [transtag more]
  


  
  divtag
     : [BEGDIV] "." [WORDDIV d] " " [items a] [ENDDIV] = <div n=[WORDDIV d] type="textpart">[items a]</>
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [items a] [ENDDIV] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[items a]</>


     : [BEGDIV] " " [items p] " " [ENDDIV] = <div type="textpart">[items p]</>

     : [BEGDIV] "." [WORDDIV d] " " [ptag p] [ENDDIV] = <div n=[WORDDIV d] type="textpart">[ptag p]</>
    >: [BEGDIV] "." [WORDDIV n] "." [WORDDIV s] " " [ptag p] [ENDDIV] = <div n=[WORDDIV n] subtype =[WORDDIV s] type="textpart">[ptag p]</>

  
  // this is equivalent to <ab> in normal L+
  ptag
    :  [BEGP] [divtag d] [ENDP] = <p>[divtag d]</>
    :  [BEGP] [items z] [ENDP] = <p>[items z]</>
    >: [BEGP] [items z] [ENDP] [ptag more] = <p>[items z]</> [ptag more]
    >: [BEGP] [items z] [ENDP] [NL] [ptag more] = <p>[items z]</> [NL] [ptag more]
    //>: [BEGP] [items z] [ENDP] [divtag more] = <p>[items z]</> [divtag more]
    //>: [BEGP] [items z] [ENDP] [NL] [divtag more] = <p>[items z]</> [NL] [divtag more]
    >: [BEGP] [ENDP] = <p></>
  
  items
    :  [item i] [items more] = [item i] [items more]
    >: [item p] = [item p]

  item
    
     : [app a] = [app a]
     : [foreign f] = [foreign f]
     : [linenumber l] = [linenumber l]
     : [del d] = [del d]
     : [term t] = [term t]
     : [lb l] = [lb l]
     : [gap g] = [gap g] 
     
     : [NL n] = [NL n]
    >: [WORDS w] = [WORDS w]
    >: [ANYLETTER a] = [ANYLETTER a]
  
  
  
  linenumber
//    :  "(" [LINENUM n] ")." " " = <milestone unit="line" n=[LINENUM n]/>
//    >: "(" [LINENUM n] ")." "div " = <milestone unit="line" n=[LINENUM n] rend="break"/>

    :  "(" [LINENUM n] ")" = <milestone unit="line" n=[LINENUM n]/>
    >: "((" [LINENUM n] "))" = <milestone unit="line" n=[LINENUM n] rend="break"/>



  
  foreign

    : "~|" [items a] "|~" [WORD lang] " " = <foreign xml:lang=[WORD lang]>[items a]</>
    : "~|" [WORDSFOREIGN a] "|~" [WORD lang] " " = <foreign xml:lang=[WORD lang]>[WORDSFOREIGN a]</>
    
    //: "~|" [WORDSFOREIGN a][items i] "|~" [WORD lang] " " = <foreign xml:lang=[WORD lang]>[WORDSFOREIGN a][items i]</>
    //: "~|" [items i][WORDSFOREIGN a] "|~" [WORD lang] " " = <foreign xml:lang=[WORD lang]>[items i][WORDSFOREIGN a]</>
  
    
  lb
    : "---" = <lb></>
  
  term
    : "<" [items def] "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t]>[items def]</>
    //with lang
    : "<" [items def] "=" [WORDSTERM t] "~" [WORD lang] ">" = <term target=[WORDSTERM t] xml:lang=[WORD lang]>[items def]</>


//    : "<" [WORDSTERM def] "=" [items t] ">" = <term target=[items t]>[WORDSTERM def]</>
    //with lang
//    : "<" [WORDSTERM def] "=" [items t] "~" [WORD lang] ">" = <term target=[items t] xml:lang=[WORD lang]>[WORDSTERM def]</>


    >: "<" [WORDSTERM def] "=" [WORDSTERM t] ">" = <term target=[WORDSTERM t]>[WORDSTERM def]</>
    //with lang
    >: "<" [WORDSTERM def] "=" [WORDSTERM t] "~" [WORD lang] ">" = <term target=[WORDSTERM t] xml:lang=[WORD lang]>[WORDSTERM def]</>


  gap
    //>: "[.?]" = <gap reason="lost" extent="unknown" unit="character"></>
    //>: ".?" = <gap reason="illegible" extent="unknown" unit="character"></>
    
    : "[...]" = <gap reason="lost" extent="unknown" unit="character"></>
    : "..." = <gap reason="illegible" extent="unknown" unit="character"></>

  del
     : "〚" [WORDS w] "〛" = <del>[WORDS w]</>
    >: "〚" [items a] "〛" = <del>[items a]</>

  
  app
  
    //no type  ??
    : [BEGOPT] [WORD a] "|:" [WORDSLR c] "|" [ENDOPT] = <app><lem resp=[WORDSLR c]>[WORD a]</></>
    : [BEGOPT] [WORD a] "|:|" [ENDOPT] = <app><lem resp="">[WORD a]</></>
    
    
    // BGU
    //: [BEGOPT] [WORD a] "|BGU:" [WORDSLR c] "|" [ENDOPT] = <app type="BGU"><lem resp=[WORDSLR c]>[WORD a]</></>
    : [BEGOPT] [items a] "|BGU:" [WORDSLR c] "|" [ENDOPT] = <app type="BGU"><lem resp=[WORDSLR c]>[items a]</></>
    : [BEGOPT] [items a] "|BGU:|" [ENDOPT] = <app type="BGU"><lem resp="">[items a]</></>
    
    
    //BGU_DDbDP
    : [BEGOPT] [WORD a] "|BGU_DDbDP:" [WORDSLR c] "|" [ENDOPT] = <app type="BGU_DDbDP"><lem resp=[WORDSLR c]>[WORD a]</></>
    : [BEGOPT] [WORD a] "|BGU_DDbDP:|" [ENDOPT] = <app type="BGU_DDbDP"><lem resp="">[WORD a]</></>
    

// DONE:
// <gap reason="lost" extent="unknown" unit="character"/> [.?]
// <milestone unit="line" n="*"/>
// <milestone unit="line" n="*" rend="break"/>
// 133 <gap reason="illegible" extent="unknown" unit="character"/> .?
// <div xml:lang="de" type="translation" xml:space="preserve">
// <p>
// <lb>
// <foreign xml:lang="*">
// <term target="*">
// 18 <term xml:lang="la">
// 13 <del>

// <app type="BGU_DDbDP">
// <app type="BGU">
// <lem resp="">

// REMAINING:
// <app>



// 41 <term> //is this really allowed?




// 106 <certainty match=".." locus="value"/> (always inside lem)
