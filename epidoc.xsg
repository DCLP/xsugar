NL = [\r\n]+
SP = [ ]
SPO = [ ]+ (MAX)
ABBR = "(  )"

BEGOPT = "<:"
ENDOPT = ":>"
BEGNUM = "<#"
ENDNUM = "#>"
BEGSEG = "<\&"
ENDSEG = "\&>"

UNDERDOT = "\u0323"
//ca. with 0 or more spaces followed by ? mark
CAUNKNOWN = "ca."[ ]*"\?"
LEADCA = "ca."[ ]*

// WORD breaks at space and WORDS allows space (therefore multiple words)
WORD = ([^ \/\\\n\r\t\[\]<>_#@~〚〛\*\&\,\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^]+) (MAX)
//WORDA does not allow underdot (\u0323 at the end) - used to make abbr work - A = abbreviation
WORDA = ([^ \/\\\n\r\t\[\]<>_#@~〚〛\*\&\,\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^\u0323]+) (MAX)
WORDS = ([^\/\\\n\r\t\[\]<>_#@~〚〛\*\&\,\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^]+) (MAX)
// wordsnum allow '/' for fractional numbers
WORDSNUM = ([^\\\n\r\t\[\]<>_#@~〚〛\*\&\,\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^]+) (MAX)

// WORDOPT breaks at space but has * so allows for no word to exist also in that position
WORDOPT = ([^ \/\\\n\r\t\[\]<>_#@~〚〛\*\&\,\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^]*) (MAX)

NUM = [0-9]+
ANYLETTER = [^\/\\\t\[\]<>_#@~〚〛\*\&\:\=\|\'\!\(\)\{\}\?\"¨΄`῾᾿῀¯≡‼´῾᾿\^]

DOT = "."
QUESTION = [\?]?

// symbolnum is for recognizing either a fraction or whole number with multiple digits
SYMBOLNUM = [0-9]+\/{1}[0-9]+|[0-9]+

// symnummulti is for recognizing multiple fractions or whole numbers with multiple digits divided by 1 or more spaces
SYMNUMMULTI = ([ ]*[0-9]+\/{1}[0-9]+[ ]+|[ ]*[0-9]+[ ]+)+

// appnum is for recognizing app_lem number
APPNUM = [0-9]+\.{1}[0-9]+

HANDSHIFT = m{1}[0-9]+

LANGLIST = ((Demotic)|(Coptic)|(Arabic)|(Nabatean)|(Aramaic))

// actual grammar starts below at file.  regular expression used are above

file : [items l] = <ab>[items l]</>
     : =

items : [item i] [items more] = [item i] [items more]  
		>: [item p] = [item p]	

linenumber : [NUM n] "." " " = <lb n=[NUM n]/>
newline : [NL n] = [NL n]

unclears_SP_non_terminal : [SP] [UNDERDOT] [unclears_SP_non_terminal mu] = "\u00A0" [unclears_SP_non_terminal mu]
								 >: [SP] [UNDERDOT] = "\u00A0"

unclears_non_terminal : [ANYLETTER a] [UNDERDOT] [unclears_non_terminal mu] = [ANYLETTER a] [unclears_non_terminal mu]
								 >: [ANYLETTER a] [UNDERDOT] = [ANYLETTER a]
		 
morenum : [BEGNUM] [WORD w] "=" [SYMBOLNUM x] [ENDNUM] [morenum y] = <num value=[SYMBOLNUM x]>[WORD w]</>  [morenum y]
			   >: [BEGNUM] [WORD w] "=" [SYMBOLNUM x] [ENDNUM] = <num value=[SYMBOLNUM x]>[WORD w]</>
		 
expand 	: [WORD a] "(" [WORD b] ")" [expand more] = [WORD a]<ex>[WORD b]</>[expand more]
			>: [WORD a] "(" [WORD b] ")" = [WORD a]<ex>[WORD b]</>
			
expandex : "(" [WORDS b] ")" [expandex more] = <ex>[WORDS b]</>[expandex more]
			>: "(" [WORDS b] ")" = <ex>[WORDS b]</>
			
expandi : [items a] "(" [WORD b] ")" [expandi more] = [items a]<ex>[WORD b]</>[expandi more]
			>: [items a] "(" [WORD b] ")" = [items a]<ex>[WORD b]</>
			>: [items a] "(" [items c] ")" = [items a]<ex>[items c]</>

item 
	: [linenumber l] = [linenumber l]
	: [newline n] = [newline n]

	//---test_abbreviation_unknown_resolution---
	// ab( )
	: [WORDA w] [ABBR] = <abbr>[WORDA w]</>
	: [WORDA w] "?" [ABBR] = <abbr cert="low">[WORDA w]</>
	>: [SP] [items w] [ABBR] = <abbr>[items w]</>

	: [expand e] = <expan>[expand e]</>
	: [expand e] [WORD a] = <expan>[expand e] [WORD a]</>
	>: [linenumber l] [expandi e] = [linenumber l] <expan>[expandi e]</>
//	>: [expandi e] = <expan>[expandi e]</>
	>: [expandi e] [WORD a] = <expan>[expandi e] [WORD a]</>
  	>: [SPO s] [expandi e] = [SPO s] <expan>[expandi e]</>
	
	//---test unclear characters = underdots
	: [unclears_non_terminal u] = <unclear>[unclears_non_terminal u]</>
	>: [unclears_SP_non_terminal u] "!" = <unclear reason="undefined">[unclears_SP_non_terminal u]</>
	>: [unclears_non_terminal u] "!" = <unclear reason="undefined">[unclears_non_terminal u]</>
	>: [unclears_SP_non_terminal u] "?" = <unclear reason="undefined" cert="low">[unclears_SP_non_terminal u]</>
	>: [unclears_non_terminal u] "?" = <unclear reason="undefined" cert="low">[unclears_non_terminal u]</>
	
	//---choice---
	//  <:a|orth|b|:> 
	: [BEGOPT] [WORD a] "?" "|orth|" [WORD b] [ENDOPT] = <choice><corr cert="low">[WORD a]</><sic>[WORD b]</></>
// the line below causes horizontal ambiguity but does what it needs to - set up for case such as   p.cair.zen/p.cair.zen.3/p.cair.zen.3.59403.xml
// will see if it handles it tomorrow and what else it does
	>: [BEGOPT] [WORD a] "?" [items c] "|orth|" [items d] [ENDOPT] = <choice><corr cert="low">[WORD a][items c]</><sic>[items d]</></>
	: [BEGOPT] [WORD a] "|orth|" [WORD b] [ENDOPT] = <choice><corr>[WORD a]</><sic>[WORD b]</></>
	>: [BEGOPT] [items a] "|orth|" [WORD b] [ENDOPT] = <choice><corr>[items a]</><sic>[WORD b]</></>
	>: [BEGOPT] [WORD a] "|orth|" [items b] [ENDOPT] = <choice><corr>[WORD a]</><sic>[items b]</></>
	>: [BEGOPT] [items a] "|orth|" [items b] [ENDOPT] = <choice><corr>[items a]</><sic>[items b]</></>
	>: [BEGOPT] "|orth|" [WORD b] [ENDOPT] = <choice><corr></><sic>[WORD b]</></>
	>: [BEGOPT] "|orth|" [items b] [ENDOPT] = <choice><corr></><sic>[items b]</></>

	//---milestone---
	//  '----' or  '--------'
	: "--------" = <milestone rend="horizontal-rule" unit="undefined"></>
	: "----" = <milestone rend="paragraphos" unit="undefined"></>

	//---test_uncertain_diacritical_diaeresis---
	// (¨)
	: [ANYLETTER a] "(¨)" = <hi rend="diaeresis">[ANYLETTER a]</>
	>: [ANYLETTER a] [UNDERDOT] "(¨)" = <hi rend="diaeresis"><unclear reason="undefined">[ANYLETTER a]</></>
	
	//---test_uncertain_diacritical_varia---
	// (`)
	: [ANYLETTER a] "(`)" = <hi rend="varia">[ANYLETTER a]</>	
	
	//---test_uncertain_diacritical_dasia---
    // (῾)
	: [ANYLETTER a] "(῾)" = <hi rend="dasia">[ANYLETTER a]</>
			
	//---test_uncertain_diacritical_psili---
	// (᾿)
	: [ANYLETTER a] "(᾿)" = <hi rend="psili">[ANYLETTER a]</>
	
	//---test_uncertain_diacritical_oxia---
	// (΄)
	: [ANYLETTER a] "(΄)" = <hi rend="oxia">[ANYLETTER a]</>
	
	//---test_uncertain_diacritical_perispomeni---
	// (῀)
	: [ANYLETTER a] "(῀)" = <hi rend="perispomeni">[ANYLETTER a]</>
	
	//---test_uncertain_diacritical_spiritus_asper--- can also be known as greek dasia when combined with space per wikipeidia
	// (῾)
	: [ANYLETTER a] "( ῾)" = <hi rend="asper">[ANYLETTER a]</>
	
	//---test_uncertain_diacritical_acute---
	// (´)
	: [ANYLETTER a] "(´)" = <hi rend="acute">[ANYLETTER a]</>
	
	//---test_uncertain_diacritical_circumflex---
	// (^)
	: [ANYLETTER a] "(^)" = <hi rend="circumflex">[ANYLETTER a]</>
	>: [ANYLETTER a] [UNDERDOT] "(^)" = <hi rend="circumflex"><unclear reason="undefined">[ANYLETTER a]</></>
	
	//---test_uncertain_diacritical_spiritus_lenis--- can also be known as greek psili when combined with space per wikipeidia
	// (᾿)
	: [ANYLETTER a] "( ᾿)" = <hi rend="lenis">[ANYLETTER a]</>
	>: [ANYLETTER a] [UNDERDOT] "( ᾿)" = <hi rend="lenis"><unclear reason="undefined">[ANYLETTER a]</></>
	
	//---test_illegible_dot_gap---
	// .1, .2, .3
	: [DOT] [NUM n] = <gap reason="illegible" extent=[NUM n] unit="character"></>  
	
	//---test_illegible_dot_max---
	// .#{n}-#{n}
	: [DOT] [NUM v] "-" [NUM w] = <gap reason="illegible" extent=[NUM v] extentmax=[NUM w] unit="character"></>
	
	//---test_illegible_dot_lin---
	// .1lin, .2lin, .3lin
	: [DOT] [NUM n] "lin" = <gap unit="line" extent=[NUM n] reason="illegible"></>  
	
	//---test_illegible_gap_ca---
	// ca.1, ca.2, ca.3
	: [LEADCA] [NUM j] = <gap reason="illegible" extent=[NUM j] unit="character" precision="circa"></>
	
	//---test_lang_gap---
	//(Lang: Demotic 2 lines)
	: "(Lang: " [LANGLIST l] " " [NUM v] " lines)" = <gap reason="ellipsis" desc=[LANGLIST l] extent=[NUM v] unit="line"></>
	
	//---test_lines_not_transcribed---
	//"lines not transcribed"
	: "(Lines: " [NUM v] " not transcribed)" = <gap reason="ellipsis" extent=[NUM v] unit="line"></>
	
	//---perpendicular_lines---
	//(23, perp)
	: "(" [NUM v] ", perp)" = <lb rend="perpendicular" n=[NUM v]/>
	
	//---inverse_lines---
	//(23, inv)
	: "(" [NUM v] ", inv)" = <lb rend="inverse" n=[NUM v]/>
	
	//---subscript---
	//   /abc\
	: "\/" [WORD a] "\\" = <hi rend="subscript">[WORD a]</>
	>: "\/" [items i] "\\" = <hi rend="subscript">[items i]</>
	
	//---superscript---?????? commented out for supralinear
	//   \|abc|/
    : "\\\|" [WORD a] "\|\/" = <hi rend="superscript">[WORD a]</>
	>: "\\\|" [items i] "\|\/" = <hi rend="superscript">[items i]</>
	
	//---add_place_supralinear---
	//   \abc/
    : "\\" [WORDS a] "\/" = <add place="supralinear">[WORDS a]</>
	: "\\" [WORDS a] "?" "\/" = <add place="supralinear" cert="low">[WORDS a]</>
	>: "\\" [items a] "\/" = <add place="supralinear">[items a]</>
	
	//---grave---
	//   \abc/
    : "\|*" [WORD a] "*\|" = <hi rend="grave">[WORD a]</>
	
	//---supraline---
	//   ¯abc¯ - uses non-combining macron
    : "¯" [WORD a] "¯" = <hi rend="supraline">[WORD a]</>
	>: "¯" [items i] "¯" = <hi rend="supraline">[items i]</>
	
	//---supraline_underline---
	//   |_abc_|
    : "≡" [WORD a] "≡" = <hi rend="supraline-underline">[WORD a]</>
		
	//---undefined_parallel---
	//  _abc_  - underlines
    : "_" [WORDS a] "_" = <supplied reason="undefined" evidence="parallel">[WORDS a]</>
	: "_" [WORDS a] "?" "_" = <supplied reason="undefined" evidence="parallel" cert="low">[WORDS a]</>
		
	//---lost_parallel---
	//  _[abc]_  - underlines
    : "_[" [WORD a] "]_" = <supplied reason="lost" evidence="parallel">[WORD a]</>
	
	//---number or fraction_no_symbol---
	//  #a#1/2# a = character representation and then the fraction it equals between next #'s
    : "<#=" [SYMBOLNUM s] [ENDNUM] = <num value=[SYMBOLNUM s]/>
	: "<#=" [SYMNUMMULTI m] "#>" = <num value=[SYMNUMMULTI m]/>
	: [BEGNUM] [WORD o] "=#>" = <num>[WORD o]</>
	
	//---fraction_number and whole_number symbol and multi_symbol---
	//  <#a#1/2#> a = symbol representation and then the number/fraction it equals after '='
    : [BEGNUM] [WORD o] "=" [SYMBOLNUM s] [ENDNUM] = <num value=[SYMBOLNUM s]>[WORD o]</>
	: [BEGNUM] [WORD o] [morenum m] "=" [SYMBOLNUM s] [ENDNUM] = <num value=[SYMBOLNUM s]>[WORD o] [morenum m]</>
	>: [BEGNUM] [items o] "=" [SYMBOLNUM s] [ENDNUM] = <num value=[SYMBOLNUM s]>[items o]</>
		
	//---subst---
	//  <:a|subst|b|:> 
	: [BEGOPT] [WORD a] "|subst|" [WORD b] "?" [ENDOPT] = <subst><add place="inline">[WORD a]</><del rend="corrected" cert="low">[WORD b]</></>
	: [BEGOPT] [WORD a] "|subst|" [WORD b] [ENDOPT] = <subst><add place="inline">[WORD a]</><del rend="corrected">[WORD b]</></>
	>: [BEGOPT] [items a] "|subst|" [WORD b] [ENDOPT] = <subst><add place="inline">[items a]</><del rend="corrected">[WORD b]</></>
	>: [BEGOPT] [WORD a] "|subst|" [items b] [ENDOPT] = <subst><add place="inline">[WORD a]</><del rend="corrected">[items b]</></>
	>: [BEGOPT] [items a] "|subst|" [items b] [ENDOPT] = <subst><add place="inline">[items a]</><del rend="corrected">[items b]</></>
	
	//---app_lem---
	//  <|abc|def|> 
	: [BEGOPT] [WORD a] "|BL:" [WORDS c] "|" [WORD b] [QUESTION q] [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[WORD a]</><rdg>[WORD b][QUESTION q]</></>
	>: [BEGOPT] [WORDOPT a] "|BL:" [WORDS c] "|" [WORDOPT b] [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[WORDOPT a]</><rdg>[WORDOPT b]</></>
	>: [BEGOPT] [items a] "|BL:" [WORDS c] "|" [WORD b] [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[items a]</><rdg>[WORD b]</></>
	>: [BEGOPT] [WORD a] "|BL:" [WORDS c] "|" [items b] [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[WORD a]</><rdg>[items b]</></>
	>: [BEGOPT] [items a] "|BL:" [WORDS c] "|" [items b] [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[items a]</><rdg>[items b]</></>
//line below added for empty tag rdg on BL
	>: [BEGOPT] [items a] "|BL:" [WORDS c] "|" [ENDOPT] = <app type="BL"><lem resp=[WORDS c]>[items a]</><rdg></></>
	: [BEGOPT] [WORD a] "|ed:" [WORDS e] "|" [WORD b] [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]>[WORD a]</><rdg>[WORD b]</></>
	>: [BEGOPT] [items a] "|ed:" [WORDS e] "|" [WORD b] [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]>[items a]</><rdg>[WORD b]</></>
	>: [BEGOPT] [WORD a] "|ed:" [WORDS e] "|" [items b] [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]>[WORD a]</><rdg>[items b]</></>
	>: [BEGOPT] [items a] "|ed:" [WORDS e] "|" [items b] [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]>[items a]</><rdg>[items b]</></>
//line below added for empty tag lem on editorial with resp
	>: [BEGOPT] "|ed:" [WORDS e] "|" [items b] [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]></><rdg>[items b]</></>
//line below added for empty tag rdg on editorial with resp
	>: [BEGOPT] [items a] "|ed:" [WORDS e] "|" [ENDOPT] = <app type="editorial"><lem resp=[WORDS e]>[items a]</><rdg></></>
	: [BEGOPT] [WORDS a] "|ed:|" [WORDS b] [ENDOPT] = <app type="editorial"><lem>[WORDS a]</><rdg>[WORDS b]</></>
	>: [BEGOPT] [items a] "|ed:|" [WORDS b] [ENDOPT] = <app type="editorial"><lem>[items a]</><rdg>[WORDS b]</></>
	>: [BEGOPT] [WORDS a] "|ed:|" [items b] [ENDOPT] = <app type="editorial"><lem>[WORDS a]</><rdg>[items b]</></>
	>: [BEGOPT] [items a] "|ed:|" [items b] [ENDOPT] = <app type="editorial"><lem>[items a]</><rdg>[items b]</></>
//line below added for empty tag rdg on editorial without resp
	>: [BEGOPT] [items a] "|ed:|" [ENDOPT] = <app type="editorial"><lem>[items a]</><rdg></></>
	: [BEGOPT] [WORD a] "|alt:|" [WORD b] [ENDOPT] = <app type="alternative"><lem>[WORD a]</><rdg>[WORD b]</></>
	>: [BEGOPT] [items a] "|alt:|" [WORD b] [ENDOPT] = <app type="alternative"><lem>[items a]</><rdg>[WORD b]</></>
	>: [BEGOPT] [WORD a] "|alt:|" [items b] [ENDOPT] = <app type="alternative"><lem>[WORD a]</><rdg>[items b]</></>
	>: [BEGOPT] [items a] "|alt:|" [items b] [ENDOPT] = <app type="alternative"><lem>[items a]</><rdg>[items b]</></>
	
	//---glyph---
	//  '‼a', '‼a,b', '‼filler(a), ‼@a , ‼a b ' 
	: "‼" [WORD a] " " = <g type=[WORD a]></>
	: "‼" [WORD a] "," [WORD b] " " = <g type=[WORD a]>[WORD b]</>
	: "‼filler(" [WORD a] ")" " " = <g type="filler" rend=[WORD a]></>
	: "‼@" [WORD a] " " = <orig><g type=[WORD a]></></>
	>: "‼" [WORDS a] " " = <g type=[WORDS a]></>
	
	//---hand_shift---
	//  $m# 
	: "$" [HANDSHIFT h] " " = <handShift new=[HANDSHIFT h]></>
	
	//---space_unknown---
	//  vac.? 
	: "vac.?" = <space extent="unknown" unit="character"></>
	
	//---note---
	//  /*abcdefg*/ 
	: "\/*" [WORDS a] [QUESTION b] "*\/" = <note lang="en">[WORDS a][QUESTION b]</>
	: "\/*" [QUESTION b] "*\/" = <note lang="en">[QUESTION b]</>
	>: "\/*" [items b] "*\/" = <note lang="en">[items b]</>
	
	//---foreign_lang---
	//  ~veni vedi vici~la 
	: "~" [WORDS a] "~" [WORD b] " " = <foreign lang=[WORD b]>[WORDS a]</>
	>: "~" [items a] "~" [WORD b] " " = <foreign lang=[WORD b]>[items a]</>
	
	//---figure---
	//  fig.seal 
	: "#" [WORD a] " " = <figure><figDesc>[WORD a]</></>
	
	//---del_rend---
	//  not brackets - already defined in inside-brackets
	: "〚X" [WORDS a] "〛" = <del rend="cross-strokes">[WORDS a]</>
	>: "〚X" [items a] "〛" = <del rend="cross-strokes">[items a]</>
// repeat of above with NL at end - if fix XML may not need these 2 lines
//	>: "〚X" [WORDS a] "〛" = <del rend="cross-strokes">[WORDS a] [NL]</>
//	>: "〚X" [items a] "〛" = <del rend="cross-strokes">[items a] [NL]</>
	: "〚\/" [WORDS a] "〛" = <del rend="slashes">[WORDS a]</>
	>: "〚\/" [items a] "〛" = <del rend="slashes">[items a]</>
// repeat of above with NL at end - if fix XML may not need these 2 lines
//	>: "〚\/" [WORDS a] "〛" = <del rend="slashes">[WORDS a] [NL]</>
//	>: "〚\/" [items a] "〛" = <del rend="slashes">[items a] [NL]</>
//this one has to be last so the first case will be caught before get here because '-' is a valid WORDS character
	>: "〚" [WORDS a] "〛" = <del rend="erasure">[WORDS a]</>
	>: "〚" [items a] "〛" = <del rend="erasure">[items a]</>
// repeat of above with NL at end - if fix XML may not need these 2 lines
//	>: "〚" [WORDS a] "〛" = <del rend="erasure">[WORDS a] [NL]</>
//	>: "〚" [items a] "〛" = <del rend="erasure">[items a] [NL]</>
	
	
	>: [flat f] = [flat f]


	
flat 
	//---test_expand low cert---
	// (?)
	: "(" "  ?" ")" = <expan><ex cert="low"></></>
	
	//---test_expand low cert---
	// (abc?)
	: "(" [WORD w] "?" ")" = <expan><ex cert="low">[WORD w]</></>
	>: [WORD a] "(" [WORD b] "?" ")" = <expan>[WORD a]<ex cert="low">[WORD b]</></>
	>: [items a] "(" [WORD b] "?" ")" = <expan>[items a]<ex cert="low">[WORD b]</></>
	
	//---test_expansion---
	// a(b)
// comment the next line - causes 5 reversability and passing errors in EXPAN in BGU.1 - bgurakcov0414_v06
//	>: [WORD a] "(" [WORD b] __")" = <expan>[WORD a]<ex>[WORD b] __</></>
	>: [WORDS a] "(" [WORDS b] ")" = <expan>[WORDS a]<ex>[WORDS b]</></>
	
	//---test_symbol_expansion---
	// (abc)
	: "(" [WORDSNUM a] ")" = <expan><ex>[WORDSNUM a]</></>
//: "(" [WORD a] __ [WORD b] ")" = <expan><ex>[WORD a] __ [WORD b]</></> - old definition of WORDSNUM i think
//	: "(" [WORD a] ")" = <expan><ex>[WORD a]</></> - commented out to remove vert ambig with wordsnum above
	: "(" "(" [WORD a] ")" [WORD c] "(" [WORD b] ")" ")" = <expan><ex>[WORD a]</>[WORD c]<ex>[WORD b]</></>
	>: [expandex e] = <expan>[expandex e]</>
	>: "(" [items a] ")" = <expan><ex>[items a]</></>

	//---test_vestige_lines---
	// vestig.#{n}lin
	: "vestig." [NUM v] "lin" = <gap reason="illegible" extent=[NUM v] unit="line" desc="vestiges"></>
	
	//---test_vestige_lines_ca---
	// vestig.ca.#{n}lin
	: "vestig.ca." [NUM v] "lin" = <gap reason="illegible" extent=[NUM v] unit="line" precision="circa" desc="vestiges"></>
	
	//---test_vestige_lines_unknown---
	// vestig.?lin
	: "vestig.?lin" = <gap reason="illegible" extent="unknown" unit="line"></>
	
	//---test_vestige_known_char---
	// vestig.!.#{n}
	: "vestig." [NUM v] "char" = <gap reason="vestig" extent=[NUM v] unit="character"></>
	
	//---test_vestige_characters---
	// vestig
	: "vestig" = <gap reason="illegible" extent="unknown" unit="character" desc="vestiges"></>
	
	//---test_lost_lines---
	// lost.#{n}lin
	: "lost." [NUM v] "lin" = <gap reason="lost" extent=[NUM v] unit="line"></>
	
	//---test_lost_lines_unknown---
	// lost.#{n}lin
	: "lost." "?" "lin" = <gap reason="lost" extent="unknown" unit="line"></>
	
	//---test_quotation_marks---
	// " "
	>: "\"" [WORDS w] "\"" = <q>[WORDS w]</>
	
	//---test_omitted---
	//< >
	>: "<" [WORDS w] ">" = <supplied reason="omitted">[WORDS w]</>
	>: "<" [WORDS w] "?" ">" = <supplied reason="omitted" cert="low">[WORDS w]</>
	>: "<" [items i] ">" = <supplied reason="omitted">[items i]</>
	
	//---test_sic---
	//sic
	>: "{" [WORDS w] "}" = <sic>[WORDS w]</>	
	>: "{" [items w] "}" = <sic>[items w]</>
	
	//---test_seg---
	//seg
	>: [BEGSEG] [items i] [ENDSEG] = <seg cert="low">[items i]</>	
	
	//---expand loop and items---
	: [WORDOPT a] "[" [WORDOPT b] "(" [WORD c] ")" [WORDOPT d] "]" [WORDOPT e] = <expan>[WORDOPT a]<supplied reason="lost">[WORDOPT b]<ex>[WORD c]</>[WORDOPT d]</>[WORDOPT e]</>
	>: [items a] "[" [WORDOPT b] "(" [WORD c] ")" [WORDOPT d] "]" [WORDOPT e] = <expan>[items a]<supplied reason="lost">[WORDOPT b]<ex>[WORD c]</>[WORDOPT d]</>[WORDOPT e]</>
	: [WORDOPT a] "[" [WORDOPT b] "(" [WORD c] "?" ")" [WORDOPT d] "]" [WORDOPT e] = <expan>[WORDOPT a]<supplied reason="lost">[WORDOPT b]<ex cert="low">[WORD c]</>[WORDOPT d]</>[WORDOPT e]</>
	
	//---multiple tests see inside_brackets production---
	// [ ], [[ ]], [ca.?] etc...
	>: "[" [inside_brackets i] "]" =  [inside_brackets i] 	
	
	//test below are after inside_brackets to keep from getting the definitions confused
	
	//---test_illegible_gap_unknown---
	// .?
	: ".?" = <gap reason="illegible" extent="unknown" unit="character"></>
	
	//---test_illegible_gap_unknown_ca---
	// ca.?
	: [CAUNKNOWN] = <gap reason="illegible" extent="unknown" unit="character" precision="circa"></>
	
	>: [ANYLETTER a] = [ANYLETTER a]
		
	
	
inside_brackets 
	
	//---test_lost_dot_gap---
	// .1, .2, .3 inside brackets
	: [DOT] [NUM n] = <gap reason="lost" extent=[NUM n] unit="character"></>
	
	//---test_lost_dot_max---
	// .#{n}-#{n}
	: [DOT] [NUM v] "-" [NUM w] = <gap reason="lost" extent=[NUM v] extentmax=[NUM w] unit="character"></>
			
	//---test_lost_gap_unknown---
	// [ .? ]  	
	>: ".?" = <gap reason="lost" extent="unknown" unit="character"></>
	
	//---test_lost_gap_unknown_ca---
	// [ ca.? ]  	
	>: [CAUNKNOWN] = <gap reason="lost" extent="unknown" unit="character" precision="circa"></>
	
	//---test_lost_words---
	// [ abc def ] lost words
	: [WORDS w] = <supplied reason="lost">[WORDS w]</>
	
	//---test_lost_uncertain---
	// [ abc? ] uncertain lost
	>: [WORDS w] "\?" = <supplied reason="lost" cert="low">[WORDS w]</>
	
	//---test_lost--- - placed at the end so all other possibilities of inside brackets are checked before looping through 'items' production
	// [ ] lost
	>: [items is] = <supplied reason="lost">[items is]</>
	// the line below causes horizontal ambiguity if you put WORD or WORDS in it.  ANYLETTER is close to WORDS because it allows spaces but not comma - when added a
// comma to ANYLETTER it really messed things up so took it back out.  ANYLETTER keeps the horizontal ambiguity out of the grammar it gets is close to WORDS in
// solving grammar (within 1 on BGU.1 so going to leave that for now.  Not sure why WORD/WORDS causes ambiguity and ANYLETTER doesn't.
	>: [ANYLETTER w] [items is] = <supplied reason="lost">[ANYLETTER w] [items is]</>
	
			
