xmlns:xml = "http://www.w3.org/XML/1998/namespace"

NL = [\r\n]+

BEGIN = "<"
END = ">"

BEGP = [\n]{2}

BEGWRAP = "<W"
ENDWRAP = "W>"

PN_URL = ((ddbdp/)|(hgv/)|(apis/))

NUM_RNG_WHL = ([0-9]+\-{1}[0-9]+|[0-9]+|[IVXLCDM]+\-{1}[IVXLCDM]+|[IVXLCDM]+|[ivxlcdm]+\-{1}[ivxlcdm]+|[ivxlcdm]+)

//same as WORDSLOOSE except for slashes and tab and no + for 1 or more and no MAX designator
ANYLETTER = [^\/\\\t<>_\:\*\|]

//very permisive for free text, only remove what is in grammar
WORDSLOOSE = ([^<>_\:\*\|]+) (MAX)

wrap 
  : [BEGWRAP] [items i] [ENDWRAP] = <wrap>[items i]</>
  
items
  :  [item i] [items more] = [item i] [items more]
  >: [item p] = [item p]

item
  : [paragraph p] = [paragraph p]
  : [footnote f] = [footnote f]
  : [bib b] = [bib b]
  : [url u] = [url u]
  : [emphasis_bold eb] = [emphasis_bold eb]
  : [emphasis_italics ei] = [emphasis_italics ei]
  : [emphasis_underline el] = [emphasis_underline el]
  : [item_can_nest icn] = [item_can_nest icn]

item_can_nest
  >: [WORDSLOOSE w] = [WORDSLOOSE w]
  >: [ANYLETTER a] = [ANYLETTER a]

paragraph_item_not_nest
  :[paragraph_item bi] [paragraph_item_not_nest more] = [paragraph_item bi] [paragraph_item_not_nest more]
  >:[paragraph_item bi] = [paragraph_item bi]
  
paragraph_item
  : [footnote f] = [footnote f]
  : [bib b] = [bib b]
  : [url u] = [url u]
  : [emphasis_bold eb] = [emphasis_bold eb]
  : [emphasis_italics ei] = [emphasis_italics ei]
  : [emphasis_underline el] = [emphasis_underline el]
  : [item_can_nest icn] = [item_can_nest icn]
  
bold_item_not_nest
  :[bold_item bi] [bold_item_not_nest more] = [bold_item bi] [bold_item_not_nest more]
  >:[bold_item bi] = [bold_item bi]
  
bold_item
  : [footnote f] = [footnote f]
  : [paragraph p] = [paragraph p]
  : [bib b] = [bib b]  
  : [url u] = [url u]
  : [emphasis_italics ei] = [emphasis_italics ei]
  : [emphasis_underline el] = [emphasis_underline el]
  : [item_can_nest icn] = [item_can_nest icn]
  
italic_item_not_nest
  :[italic_item ii] [italic_item_not_nest more] = [italic_item ii] [italic_item_not_nest more]
  >:[italic_item ii] = [italic_item ii]
  
italic_item
  : [footnote f] = [footnote f]
  : [paragraph p] = [paragraph p]
  : [bib b] = [bib b]  
  : [url u] = [url u]
  : [emphasis_bold eb] = [emphasis_bold eb]
  : [emphasis_underline el] = [emphasis_underline el]
  : [item_can_nest icn] = [item_can_nest icn]
  
underline_item_not_nest
  :[underline_item ui] [underline_item_not_nest more] = [underline_item ui] [underline_item_not_nest more]
  >:[underline_item ui] = [underline_item ui]
  
underline_item
  : [footnote f] = [footnote f]
  : [paragraph p] = [paragraph p]
  : [bib b] = [bib b]  
  : [url u] = [url u]
  : [emphasis_bold eb] = [emphasis_bold eb]
  : [emphasis_italics ei] = [emphasis_italics ei]
  : [item_can_nest icn] = [item_can_nest icn]
  
paragraph
  : [BEGP] [paragraph_item_not_nest pinn] = <p>[paragraph_item_not_nest pinn]</>
  
footnote
  : "<:fn=" [WORDSLOOSE text] ":>" = <note type="footnote" xml:lang="en">[WORDSLOOSE text]</>
  
pn_target
  : [PN_URL pnurl][WORDSLOOSE url] = "http://papyri.info/" [PN_URL pnurl][WORDSLOOSE url]
  >: [WORDSLOOSE url] = "http://" [WORDSLOOSE url]
  
url
  : "<:" [WORDSLOOSE text] "|" [pn_target pt] ":>" = <ref target=[pn_target pt]>[WORDSLOOSE text]</>

bib_target
  : "bibl/" [WORDSLOOSE url] = "http://" [WORDSLOOSE url] 
    
bib_scope
  : [bib_scope_detail bsd] [bib_scope more] = [bib_scope_detail bsd] [bib_scope more]
  : [bib_scope_detail bsd] = [bib_scope_detail bsd]
  : =
  
bib_scope_detail
  : "|v=" [NUM_RNG_WHL nrw] = <biblScope type="vol">[NUM_RNG_WHL nrw]</>
  : "|i=" [NUM_RNG_WHL nrw] = <biblScope type="issue">[NUM_RNG_WHL nrw]</>
  : "|p=" [NUM_RNG_WHL nrw] = <biblScope type="pp">[NUM_RNG_WHL nrw]</>
  : "|l=" [NUM_RNG_WHL nrw] = <biblScope type="ll">[NUM_RNG_WHL nrw]</>
  : "|ch=" [NUM_RNG_WHL nrw] = <biblScope type="chap">[NUM_RNG_WHL nrw]</>
  
bib
  : "<:" [WORDSLOOSE text] "|" [bib_target bt] [bib_scope bs] ":>" = <listBibl><bibl><ref target=[bib_target bt]>[WORDSLOOSE text]</>[bib_scope bs]</></>
  
emphasis_bold
  : "*" [bold_item_not_nest binn] "*" = <emph rend="bold">[bold_item_not_nest binn]</>

emphasis_italics
  : "|" [italic_item_not_nest iinn] "|" = <emph rend="italics">[italic_item_not_nest iinn]</>

emphasis_underline
  : "_" [underline_item_not_nest uinn] "_" = <emph rend="underline">[underline_item_not_nest uinn]</>


